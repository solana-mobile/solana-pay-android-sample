/*
 * Copyright (c) 2022 Solana Labs, Inc.
 */

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf' version "0.8.18"
}

android {
    compileSdk 31

    defaultConfig {
        minSdk 23
        targetSdk 31

        consumerProguardFiles "consumer-rules.pro"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    sourceSets {
        test {
            proto {
                srcDir 'external/digitalassetlinks/compatibility-tests/v1'
            }
        }
    }
}

protobuf {
    protoc {
        def archSuffix = ''
        if (osdetector.os == "osx") {
            // Workaround: there are no published protoc binaries for Apple Silicon-based macs.
            // Apply a manual architecture to the artifact to select the osx-x86_64 variant, which
            // runs fine with Rosetta.
            archSuffix = ':osx-x86_64'
        }
        artifact = "com.google.protobuf:protoc:3.20.0$archSuffix"   // Download from repositories
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { }
            }
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.collection:collection:1.2.0'
    testImplementation 'com.google.protobuf:protobuf-java:3.20.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-inline:4.5.1'
    testImplementation 'org.robolectric:robolectric:4.8'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'junit:junit:4.13.2'
}